apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    compileSdk 35
    ndkVersion '28.1.13356709'
    
    defaultConfig {
        applicationId 'io.tqjxlm.sparkle'
        minSdk 31
        targetSdk 35
        externalNativeBuild {
            cmake {
                def fixedArgs = [
                    "-DANDROID_ABI=arm64-v8a"
                    , "-DANDROID_PLATFORM=35"
                    , "-DANDROID=TRUE"
                    ]

                arguments = fixedArgs + (project.hasProperty('cmakeArgs') ? Arrays.asList(project.cmakeArgs.split(' ')) : [])
                abiFilters "arm64-v8a"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled = true
            proguardFiles getDefaultProguardFile('proguard-android.txt')
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "arm64-v8a"
            }
            signingConfig signingConfigs.debug
        }
        debug {
            packaging {
                jniLibs {
                    // keep debug symbols so cpptrace can work
                    useLegacyPackaging = true
                    keepDebugSymbols += ['**/*.so']
                }
            }
            ndk {
                //noinspection ChromeOsAbiSupport
                abiFilters "arm64-v8a"
            }
        }
    }

    externalNativeBuild {
        cmake {
            path '../../../CMakeLists.txt'
            buildStagingDirectory "${rootProject.projectDir}/output"
        }
    }

    buildFeatures {
        prefab true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    sourceSets {
        main {
            assets.srcDirs = ['src/main/assets']
        }
    }
    
    namespace 'io.tqjxlm.sparkle'
}

// Generate directory manifest files for Android Asset Manager
// Android's AAssetDir_getNextFileName() only returns files, not subdirectories.
// This task creates _dir_manifest.txt files listing subdirectories for each directory.
tasks.register('generateAssetManifests') {
    def assetsDir = file("${rootProject.projectDir}/output/assets")

    doLast {
        if (!assetsDir.exists()) {
            println "Assets directory does not exist yet: ${assetsDir}"
            return
        }

        // Recursively find all directories and generate manifests
        assetsDir.eachDirRecurse { dir ->
            def subdirs = []
            dir.eachDir { subdir ->
                subdirs.add(subdir.name)
            }

            def manifestFile = new File(dir, "_dir_manifest.txt")
            if (!subdirs.isEmpty()) {
                // Write manifest with subdirectory names
                manifestFile.text = subdirs.sort().join('\n') + '\n'
                println "Generated manifest: ${manifestFile.path} with ${subdirs.size()} entries"
            } else if (manifestFile.exists()) {
                // Remove stale manifest if no subdirectories exist
                manifestFile.delete()
            }
        }

        // Also generate manifest for the root assets directory
        def rootSubdirs = []
        assetsDir.eachDir { subdir ->
            rootSubdirs.add(subdir.name)
        }
        if (!rootSubdirs.isEmpty()) {
            def rootManifest = new File(assetsDir, "_dir_manifest.txt")
            rootManifest.text = rootSubdirs.sort().join('\n') + '\n'
            println "Generated root manifest: ${rootManifest.path} with ${rootSubdirs.size()} entries"
        }
    }
}

// keep assets in sync with the output directory
tasks.register('copyAssets', Copy) {
    from "${rootProject.projectDir}/output/assets"
    into "${project.projectDir}/src/main/assets"

    doFirst {
        def destDir = file("${project.projectDir}/src/main/assets")
        if (destDir.exists()) {
            println "Clearing target directory: ${destDir}"
            destDir.deleteDir()
        }
    }

    dependsOn(lintVitalAnalyzeRelease)
    dependsOn(generateReleaseLintVitalReportModel)
    dependsOn(generateAssetManifests)

    inputs.dir "${rootProject.projectDir}/output/assets"
    outputs.dir "${project.projectDir}/src/main/assets"
}

// make sure copyAssets runs before the mergeAssets task and after the externalNativeBuild task
android.applicationVariants.configureEach { variant ->
    // generateAssetManifests must run after native build (which copies resources)
    tasks.named("generateAssetManifests").configure {
        dependsOn tasks.named("externalNativeBuild${variant.name.capitalize()}")
    }
    tasks.named("copyAssets").configure {
       dependsOn tasks.named("externalNativeBuild${variant.name.capitalize()}")
    }
    tasks.named("merge${variant.name.capitalize()}Assets").configure {
        dependsOn tasks.named("copyAssets")
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.games:games-activity:4.0.0'
}

